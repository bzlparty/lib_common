load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@io_bazel_stardoc//stardoc:stardoc.bzl", "stardoc")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@mgred_rules_pandoc//pandoc:defs.bzl", "pandoc")
load("//:defs_test.bzl", "defs_test_suite")

exports_files(["defs.bzl"])

bzl_library(
    name = "defs",
    srcs = ["defs.bzl"],
    visibility = ["//visibility:public"],
)

defs_test_suite(name = "defs_test")

stardoc(
    name = "docs_src",
    out = "_docs.md",
    input = "defs.bzl",
)

diff_test(
    name = "docs_diff_test",
    failure_message = "Please run: bazel run //:docs_update",
    file1 = "docs.md",
    file2 = "_docs.md",
)

write_file(
    name = "gen_docs_update",
    out = "docs_update.sh",
    content = [
        "#!/usr/bin/env bash",
        "cd $BUILD_WORKSPACE_DIRECTORY",
        "cp -vf bazel-bin/_docs.md docs.md",
    ],
)

sh_binary(
    name = "docs_update",
    srcs = ["docs_update.sh"],
    data = ["_docs.md"],
)

write_file(
    name = "release",
    out = "release.sh",
    content = [
        "#!/usr/bin/env bash",
        "cd $BUILD_WORKSPACE_DIRECTORY",
        "git tag --annotate $1 && git push --tags",
    ],
    is_executable = True,
)

pandoc(
    name = "docs_html",
    out = "docs.html",
    args = [
        "--shift-heading-level-by",
        "1",
    ],
    input = "_docs.md",
    standalone = True,
    title = "bazel_lib",
    toc = True,
)

test_suite(
    name = "all_tests",
    tests = [
        "defs_test",
        "docs_diff_test",
    ],
)

alias(
    name = "docs",
    actual = "docs_html",
)
